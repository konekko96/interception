apply plugin: 'groovy'

if (DEBUG == "false") {
    apply plugin: 'com.github.dcendents.android-maven'
} else {
    apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
}

group = 'com.github.zskpaco.interception'

repositories {
    google()
    mavenCentral()
    jcenter()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation gradleApi()
    implementation localGroovy()
    implementation 'com.android.tools.build:gradle:3.1.0'
    implementation 'org.ow2.asm:asm:6.1'
}

import org.apache.tools.ant.filters.ReplaceTokens

task setVersionToClass(type: Copy) {
    from 'src/main/templates/Version.java'
    into 'build/generated-src/main/java/com/zskpaco/interception/plugin'
    filter(ReplaceTokens, tokens: [version: DEBUG == "false" ? RELEASE_VERSION : POM_VERSION_NAME, debug: DEBUG == "false" ? "false" : "true"])
    outputs.upToDateWhen { false }
}

compileJava.dependsOn setVersionToClass

sourceSets {
    main {
        java {
            srcDir 'build/generated-src/main/java'
        }
    }
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"
